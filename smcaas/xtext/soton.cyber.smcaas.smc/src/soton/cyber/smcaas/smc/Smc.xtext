grammar soton.cyber.smcaas.smc.Smc with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate smc "http://www.cyber.soton/smcaas/smc/Smc"

Smc:
	blocks+=BlockSMC* &
	main=MainSMC?
;
	
BlockSMC:
	'block' type=BlockType name=ID '=' 'new' '(' ')'';'
;


enum BlockType:
	INSERT = 'insert_data' | COMP = 'math_computation' | SEARCH = 'search' | ANONYMIZATION = 'anonymization' | 
	ACCESS = 'access_control' | PERMISSION = 'permission_release'
;

MainSMC:
	'main' {MainSMC} '{'
		commands+=Command*
	'}'
;

/* Command */

Command:
	Block | VariableDecl  | VariableAssignment | IfThenElse | While | Print | InvocationVoid | ParamDecl | Return
;

Return returns Command:
	{Return} 'return' ';'
;

ParamDecl:
	'parameter' name=ID '=' '<' stype=SecType btype=BasicType '>' parName=STRING ';'
;

InvocationVoid: 
	call=Invocation ';'
;

Block returns Command: 
	{Block} '{' ( commands+=Command )* '}'
;


Print:
	'print' '(' value = Expression ')' ';'
;

While:
	'while' '(' condition=Expression ')' body=Command
;

IfThenElse:
	'if' '(' condition=Expression ')' thenBrach=Command (=> 'else' elseBranch=Command )?
;

VariableDecl: 
	'var' visibility=SecType type=BasicType (array ?='[' (length=INT)? ']')? name=ID ('=' option=AbstractAssignment)? ';'
;

enum SecType:
	PUBLIC='public' | PRIVATE='private'
;

enum BasicType: 
	INT | DOUBLE | BOOLEAN | STRING | ENCRYPTED
;

VariableAssignment: 
	var=[VariableDecl] '=' option=AbstractAssignment ';'
;

AbstractAssignment:
	Expression | Download
;

Download:
	Client | Database
;

Database:
	'retrieveFromDB' '(' tbl=Atomic',' clm=STRING ')'
;

Client:
	'retrieveFromClient' '(' arg=STRING ')'
;

/* Expression */

Expression:
	Or 
;

Or returns Expression:
  And ({Or.left=current} "||" right=And)* ;

And returns Expression:
  Equality ({And.left=current} "&&" right=Equality)* ;

Equality returns Expression:
  Comparison (
    {Equality.left=current} op=("=="|"!=")
    right=Comparison
  )* ;

Comparison returns Expression:
  PlusOrMinus (
    {Comparison.left=current} op=(">="|"<="|">"|"<")
    right=PlusOrMinus
  )* ;

PlusOrMinus returns Expression:
  MulOrDiv (
    {PlusOrMinus.left=current} op=('+'|'-')
    right=MulOrDiv
  )* ;

MulOrDiv returns Expression:
  Primary (
    {MulOrDiv.left=current} op=('*'|'/')
    right=Primary
  )*
;

Primary returns Expression:
  '(' Expression ')' |
  {Not} "!" expression=Primary |
  Atomic
;


Atomic returns Expression:
	{IntLiteral} value=INT
	| {DoubleLiteral} value=REAL
	| {BooleanLiteral} value=BOOLEAN
	| {StringLiteral} value=STRING
	| {DateLiteral} value=DATE
	| {TimeLiteral} value=TIME
	| {VariableRef} variable=[VariableDecl]
	| Tuple
	| List
	| Dict
	| Invocation
;

Tuple:
	'tuple' '<' arg1=Atomic ',' arg2=Atomic '>'
;

List:
	'list' '(' (args+=Atomic (',' args+=Atomic)*) ')'
;

Dict:
	'dict' '(' key=Atomic ',' value=[List] ')'
;

Invocation: 
	blockName=[BlockSMC] '.' funcName=Functions
;

Functions:
	CreateTable | CheckTable | AddValues | BloomFilter | Search | AccessControl | Computation
;

/* Block Computation */

Computation:
	Count | Average | WeightedAvg | Median | Multiplication
;


Multiplication:
	'multiplication' '(' x=[VariableDecl] ',' y=[VariableDecl] ')'
;

Median:
	'median' '(' array=[VariableDecl] ')'
;

WeightedAvg:
	'w_avg' '(' weights=[VariableDecl] ',' elems=[VariableDecl] ')' 
;

Average:
	'avg' '(' array=[VariableDecl] ')'
;

Count:
	'count' '(' array=[VariableDecl] ')'
;

/* Block Access Control */

AccessControl:
	Covered | BellLapadula
;

BellLapadula:
	'accessControlBLP' '(' (cur=[VariableDecl] ',')?  mode=STRING ',' c_lvls=[VariableDecl] ',' v_lvl=[VariableDecl] ')'
;

Covered:
	'accessControlCovered' '(' match=[VariableDecl] ',' covered=[VariableDecl] ',' c_lvls=[VariableDecl] ',' v_lvl=[VariableDecl] ')'
;

/* Block Search */

Search:
	'search' '(' tblname=[VariableDecl] ',' column=STRING ',' keyword=[VariableDecl] ')'
;

BloomFilter:
	'bloomFilter' '(' pre=[VariableDecl] ',' post=[VariableDecl] ')'
;

/* Block Insert Data */

CheckTable:
	'checkDataset' '(' tblname=[VariableDecl] ')'
;

AddValues:
	'addValues' '(' tblname=[VariableDecl] (',' args+=[VariableDecl] (',' args+=[VariableDecl])*)+ ')'
;

CreateTable:
	'createDataset' '(' tblname=[VariableDecl] (',' params+=[ParamDecl] (',' params+=[ParamDecl])*)+ ')'
;

//Standard types
terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false';

/* NB INTEGER UNSIGNED! */
@Override
terminal INT returns ecore::EInt:
	('0'..'9')+;

terminal DATE:
	(INT '/' INT '/' INT);

terminal TIME:
	(INT ':' INT ':' INT);

terminal REAL returns ecore::EDouble:
	('+' | '-')? ('0'..'9')* ('.' ('0'..'9')+)?;

@Override
terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | '"' | "'" | '\\') | !('\\' | '"' | '/'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | '"' | "'" | '\\') | !('\\' | "'" | '/'))* "'"; 

@Override	
terminal ML_COMMENT:
	'/*'->'*/';

@Override
terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;